developer="http://indiecomputing.com/"
url="http://ubos.net/"
maintainer=${developer}
pkgname=$(basename $(pwd))
pkgver=0.10
pkgrel=1
pkgdesc='UBOS Python utilities'
arch=('any')
url=${developer}
license=('AGPL')
depends=(
    'python'
    'python-systemd'
)
makedepends=(
    'python-build'
    'python-hatchling'
    'python-installer'
    'python-wheel'
    'rsync'
)
backup=(
    'etc/ubos/log-default-python.conf'
    'etc/ubos/log-default-v1-python.conf'
    'etc/ubos/log-default-v2-python.conf'
    'etc/ubos/log-system-python.conf'
)

prepare() {
    cd "${srcdir}"
    [ -d "${pkgname}-${pkgver}" ] || mkdir "${pkgname}-${pkgver}"

    cd "${pkgname}-${pkgver}"
    rsync -a --delete ${startdir}/python/ ./

    cat > pyproject.toml <<EOF
[build-system]
requires = ["hatchling"]
build-backend = "hatchling.build"

[project]
name = "${pkgname}"
version = "${pkgver}"
description = "${pkgdesc}"
EOF
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    python -m build --wheel --no-isolation
}

package() {
# Conf files
    install -m644 -D ${startdir}/etc/ubos/*.conf -t ${pkgdir}/etc/ubos/

# Code
    cd "${srcdir}/${pkgname}-${pkgver}"
    python -m installer --destdir="${pkgdir}" dist/*.whl
}
